#-------------------------------------- ANNONCE & PARTICULES --------------------------------------------

function pitchout:game/events/map2/old_gods_artefact/item_management

execute as @e[type=area_effect_cloud,tag=OTV] store result score @s po.tech.global_timer run data get entity @s Age
execute unless entity @a[team=!hub] run scoreboard players set objet po.tech.global_timer 0
execute if score select po.map.vote_id matches 2 if score eventPartie po.legacy.eventtype matches 5 if entity @a[team=!hub,gamemode=!creative] run scoreboard players add objet po.tech.global_timer 1


execute at @e[type=area_effect_cloud,tag=OTV,tag=OK] run particle dragon_breath ~ ~0.05 ~ 1.5 0.09 1.5 0.0002 30 force

execute at @e[type=area_effect_cloud,tag=OTV,tag=OK] run particle witch ~ ~ ~ 0.2 0.8 0.2 0.000002 20 force

execute if score objet po.tech.global_timer matches 1 run tellraw @a [{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"light_purple"},{"text":"\n"},{"text":"::::","obfuscated":true,"color":"gold"},{"text":" objet touché par le vide","obfuscated":false,"color":"light_purple","bold": true},{"text":" offert dans ","obfuscated":false,"color":"aqua"},{"text":"2 minutes ","obfuscated":false,"color":"dark_green","bold": true},{"text":"::::","obfuscated":true,"color":"gold"},{"text":"\n"},{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"light_purple"}]
execute if score objet po.tech.global_timer matches 601 run tellraw @a [{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"light_purple"},{"text":"\n"},{"text":"::::","obfuscated":true,"color":"gold"},{"text":" objet touché par le vide","obfuscated":false,"color":"light_purple","bold": true},{"text":" offert dans ","obfuscated":false,"color":"aqua"},{"text":"1 minute 30 ","obfuscated":false,"color":"green","bold": true},{"text":"::::","obfuscated":true,"color":"gold"},{"text":"\n"},{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"light_purple"}]
execute if score objet po.tech.global_timer matches 1201 run tellraw @a [{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"light_purple"},{"text":"\n"},{"text":"::::","obfuscated":true,"color":"gold"},{"text":" objet touché par le vide","obfuscated":false,"color":"light_purple","bold": true},{"text":" offert dans ","obfuscated":false,"color":"aqua"},{"text":"1 minute ","obfuscated":false,"color":"green","bold": true},{"text":"::::","obfuscated":true,"color":"gold"},{"text":"\n"},{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"light_purple"}]
execute if score objet po.tech.global_timer matches 1801 run tellraw @a [{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"light_purple"},{"text":"\n"},{"text":"::::","obfuscated":true,"color":"gold"},{"text":" objet touché par le vide","obfuscated":false,"color":"light_purple","bold": true},{"text":" offert dans ","obfuscated":false,"color":"aqua"},{"text":"30 secondes ","obfuscated":false,"color":"gold","bold": true},{"text":"::::","obfuscated":true,"color":"gold"},{"text":"\n"},{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"light_purple"}]
execute if score objet po.tech.global_timer matches 2201 run tellraw @a [{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"light_purple"},{"text":"\n"},{"text":"::::","obfuscated":true,"color":"gold"},{"text":" objet touché par le vide","obfuscated":false,"color":"light_purple","bold": true},{"text":" offert dans ","obfuscated":false,"color":"aqua"},{"text":"10 secondes ","obfuscated":false,"color":"dark_red","bold": true},{"text":"::::","obfuscated":true,"color":"gold"},{"text":"\n"},{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"light_purple"}]
execute if score objet po.tech.global_timer matches 2400 run tellraw @a [{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"dark_red"},{"text":"\n"},{"text":"::::","obfuscated":true,"color":"gold"},{"text":" C'Thun","obfuscated":false,"bold":true,"color":"light_purple"},{"text":" vous propose un ","obfuscated":false,"color":"gold"},{"text":"objet touché par le vide","obfuscated":false,"bold":true,"color":"dark_purple"},{"text":" ::::","obfuscated":true,"color":"gold"},{"text":"\n"},{"text":"30 secondes avant disparition...","color": "aqua"},{"text":"\n"},{"text":"-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-_-","color":"dark_red"}]

#--------------------------------------- Summon and Spreadplayers ------------------------------------------------------------

execute if score objet po.tech.global_timer matches 2400 run summon minecraft:area_effect_cloud 618 37 101 {Duration:610,Tags:["OTV"]}
execute if score objet po.tech.global_timer matches 2400 run spreadplayers 587 96 5 30 false @e[type=minecraft:area_effect_cloud,tag=OTV,limit=1]

execute as @e[type=area_effect_cloud,scores={po.tech.layer=..47},tag=OTV,tag=!OK] at @s if block ~ ~ ~ air if block ~ ~1 ~ air run tag @s add OK
execute as @e[type=area_effect_cloud,scores={po.tech.layer=48..},tag=OTV,tag=!OK] run spreadplayers 587 96 5 30 false @s

execute at @e[type=area_effect_cloud,tag=OTV,tag=OK,tag=!affich] run setblock ~ ~ ~ magenta_shulker_box
#--------------------------------------- Selection Items ------------------------------------------------------------

execute unless entity @a[team=naga] if score objet po.tech.global_timer matches 2399 store result score itemselec po.tech.global_timer run random value 1..3
execute if entity @a[team=naga] if score objet po.tech.global_timer matches 2399 store result score itemselec po.tech.global_timer run random value 2..3

#--------------------------------------- Affichage Items ------------------------------------------------------------

execute at @e[type=area_effect_cloud,tag=OTV,tag=OK,tag=!affich] if score itemselec po.tech.global_timer matches 1 run summon item ~ ~1 ~ {CustomName:'{"text":"Médaillon d\'Azshara","bold":true,"color":"blue"}',CustomNameVisible:1b,PickupDelay:-1,Tags:["vide"],Item:{count:1,id:"nautilus_shell",components:{custom_name:'[{"text":"Medaillon d\'Azshara","color":"blue","bold":true}]',lore:['[{"text":"Jettez cet item au sol ","color":"yellow"},{"text":"pour vous transformer en ","color":"dark_purple"},{"text":"naga","color":"aqua","bold":true},{"text":"","color":"aqua","bold":false}]','[{"text":"avec un autre ","color":"dark_purple"},{"text":"joueur aléatoire","color":"light_purple"}]'],enchantments:{levels:{aqua_affinity:1}}}}}
execute at @e[type=area_effect_cloud,tag=OTV,tag=OK,tag=!affich] if score itemselec po.tech.global_timer matches 2 run summon item ~ ~1 ~ {CustomName:'[{"text":"AAA","bold": true,"obfuscated": true,"color":"black"},{"text":" Xal\'Atath ","bold":true,"obfuscated": false,"color":"dark_purple"},{"text":"AAA","bold": true,"obfuscated": true,"color":"black"}]',CustomNameVisible:1b,PickupDelay:-1,Tags:["vide"],Item:{count:1,id:"netherite_sword",components:{custom_name:'[{"text":":::","obfuscated": true,"color":"black"},{"text": "Xal\'Atath" ,"color":"dark_purple","bold":true},{"text":":::","obfuscated": true,"color":"black"}]',lore:['[{"text":"Cette dague, ","color":"light_purple"},{"text":"remplace votre pelle","color":"dark_blue"},{"text":" jusqu\'à votre ","color":"dark_purple"},{"text":"prochaine mort","color":"red"},{"text":" ou jusqu\'à ce que vous ","color":"dark_purple"},{"text":"tuiez quelqu\'un.","color":"green"}]','[{"text":"Elle possede un recul de rang S+. ","color":"aqua"},{"text":"Si vous tuez quelq\'un avec cette arme il perdra 2 vies.","color":"dark_green"}]','[{"text":"Si vous mourrez en possetion de cette arme vous perdez 2 vies.","color":"dark_red"}]','[{"text":"Cette arme provoque en vous un profond malaise ce qui vous donne parfois la nausée. ","color":"gold"}]'],enchantments:{levels:{aqua_affinity:1}}}}}
execute at @e[type=area_effect_cloud,tag=OTV,tag=OK,tag=!affich] if score itemselec po.tech.global_timer matches 3 run summon item ~ ~1 ~ {CustomName:'{"text":"Oeil de C\'Thun","bold":true,"color":"light_purple"}',CustomNameVisible:1b,PickupDelay:-1,Tags:["vide"],Item:{count:1,id:"ender_eye",components:{custom_name:'[{"text":"Oeil de C\'Thun","color":"light_purple","bold":true}]',lore:['[{"text":"Jettez cet item au sol ","color":"yellow"},{"text":"pour donner ","color":"dark_purple"},{"text":"à tous les joueurs","color":"aqua","bold":true},{"text":"","color":"aqua","bold":false}]','[{"text":"la Vision de C\'Thun","color":"light_purple"},{"text":" pendant 30 secondes","color":"aqua"}]'],enchantments:{levels:{aqua_affinity:1}}}}}
execute as @e[type=area_effect_cloud,tag=OTV,tag=OK,tag=!affich] run tag @s add affich

execute as @a[scores={po.misc.otv=1..}] if score itemselec po.tech.global_timer matches 1 run tellraw @a[team=!hub] ["",{"text":"::::","obfuscated":true,"color":"gold"},{"text":" ","color":"gold"},{"selector":"@s","bold": true},{"text":" a récupéré le ","color":"aqua"},{"text":"Médaillon d'Azshara","color":"blue","bold": true},{"text":" ! ","color":"aqua"},{"text":"::::","obfuscated":true,"color":"gold"}]
execute as @a[scores={po.misc.otv=1..}] if score itemselec po.tech.global_timer matches 2 run tellraw @a[team=!hub] ["",{"text":"::::","obfuscated":true,"color":"gold"},{"text":" ","color":"gold"},{"selector":"@s","bold": true},{"text":" a récupéré ","color":"aqua"},{"text":"AAA","bold": true,"obfuscated": true,"color":"black"},{"text": " Xal'Atath " ,"color":"dark_purple","bold":true},{"text":"AAA","bold": true,"obfuscated": true,"color":"black"},{"text":" ! ","color":"aqua"},{"text":"::::","obfuscated":true,"color":"gold"}]
execute as @a[scores={po.misc.otv=1..}] if score itemselec po.tech.global_timer matches 3 run tellraw @a[team=!hub] ["",{"text":"::::","obfuscated":true,"color":"gold"},{"text":" ","color":"gold"},{"selector":"@s","bold": true},{"text":" a récupéré l' ","color":"aqua"},{"text":"Oeil de C'Thun","color":"blue","bold": true},{"text":" ! ","color":"aqua"},{"text":"::::","obfuscated":true,"color":"gold"}]

#-------------------------------------- DISPARITION --------------------------------------------

execute as @e[type=area_effect_cloud,tag=OTV] at @s if score @s po.tech.global_timer matches 608.. run fill ~ ~ ~ ~ ~ ~ air replace magenta_shulker_box
execute as @e[type=area_effect_cloud,tag=OTV] if score @s po.tech.global_timer matches 608.. at @s run kill @e[type=item,tag=vide,distance=..3]
execute as @e[type=area_effect_cloud,tag=OTV,scores={po.tech.global_timer=608..}] run kill @s

execute as @a[scores={po.misc.otv=1..}] as @e[type=area_effect_cloud,tag=OTV,tag=OK] at @s run fill ~ ~ ~ ~ ~ ~ air replace magenta_shulker_box
execute as @a[scores={po.misc.otv=1..}] as @e[type=area_effect_cloud,tag=OTV,tag=OK] at @s run kill @e[type=item,tag=vide,distance=..3]
execute as @a[scores={po.misc.otv=1..}] as @e[type=area_effect_cloud,tag=OTV,tag=OK] run kill @s

#-------------------------------------- DISTRIBUTION DES OBJETS --------------------------------------------

execute as @a[scores={po.misc.otv=1..}] if score itemselec po.tech.global_timer matches 1 run item replace entity @s hotbar.2 with minecraft:nautilus_shell[minecraft:enchantments={levels:{"minecraft:aqua_affinity":1},show_in_tooltip:false},minecraft:custom_name='[{"text":"Medaillon d\'Azshara","bold":true,"color":"blue"}]',minecraft:lore=['[{"text":"Jetez cet item au sol ","color":"yellow"},{"text":"pour vous transformer en ","color":"dark_purple"},{"text":"naga","bold":true,"color":"aqua"}]','[{"text":"avec un autre","color":"dark_purple"},{"text":" joueur aléatoire","color":"light_purple"}]']] 1
execute as @a[scores={po.misc.otv=1..}] if score itemselec po.tech.global_timer matches 2 run item replace entity @s hotbar.0 with minecraft:netherite_sword[minecraft:unbreakable={},minecraft:enchantments={levels:{"minecraft:knockback":19},show_in_tooltip:false},minecraft:custom_name='[{"text":"AAA ","bold":true,"obfuscated":true,"color":"black"},{"text":"Xal\'Atath","bold":true,"obfuscated":false,"color":"dark_purple"},{"text":" "},{"text":"AAA","bold":true,"obfuscated":true,"color":"black"}]',minecraft:lore=['[{"text":"Cette dague,","color":"light_purple"},{"text":" remplace votre pelle ","color":"dark_blue"},{"text":"jusqu\'à votre","color":"dark_purple"},{"text":" ","color":"dark_blue"},{"text":"prochaine mort","color":"red"}]','[{"text":"ou jusqu\'à ce que vous ","color":"dark_purple"},{"text":"tuiez quelqu\'un.","color":"green"}]','[{"text":"Elle possède un recul de rang S+.","color":"aqua"}]','[{"text":"Si vous tuez quelq\'un avec cette arme il perdra 2 vies.","color":"dark_green"}]','[{"text":"Si vous mourrez en possession de cette arme vous perdez 2 vies.","color":"dark_red"}]','[{"text":"Cette arme provoque en vous un profond malaise ce qui vous donne parfois la nausée.","color":"gold"}]']] 1
execute as @a[scores={po.misc.otv=1..}] if score itemselec po.tech.global_timer matches 3 run item replace entity @s hotbar.2 with minecraft:ender_eye[food={nutrition:0,saturation:0,can_always_eat:true,eat_seconds:0.09},minecraft:enchantments={levels:{"minecraft:fortune":1},show_in_tooltip:false},minecraft:custom_name='[{"text":"Oeil de C\'Thun","bold":true,"color":"light_purple"}]',minecraft:lore=['[{"text":"Jettez cet item au sol ","color":"yellow"},{"text":"pour donner ","color":"dark_purple"},{"text":"à tous les joueurs","color":"aqua"}]','[{"text":"la Vision de C\'Thun ","bold":true,"color":"light_purple"},{"text":"pendant 30 secondes","color":"aqua"}]']] 1

execute as @a[scores={po.misc.otv=1..}] run scoreboard players set @s po.misc.otv 0

execute if score objet po.tech.global_timer matches 2401.. run scoreboard players set objet po.tech.global_timer 2


